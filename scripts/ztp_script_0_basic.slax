version 1.1;
/* ------------------------------------------------------------------ */
/* This program performs Zero touch provisioning for the ACX platform */
/* It will load global configuration template from a remote location  */
/* specified in the original configuration file loaded as a result    */
/* of the autoconfiguration process.                                  */
/* */
/* Version 1.1 Vasily Mukhin vmukhin@juniper.net                      */
/* Based on jctyztp script by Jeremy Schulman and Brian Sherwood      */
/* */
/* ------------------------------------------------------------------ */
/* XML namespaces */
/* ------------------------------------------------------------------ */
/* Juniper */
ns junos = "http://xml.juniper.net/junos/*/junos";
ns xnm = "http://xml.juniper.net/xnm/1.1/xnm";
ns jcs = "http://xml.juniper.net/junos/commit-scripts/1.0";
ns exsl extension = "http://exslt.org/common";
/* EXSLT */
ns str extension = "http://exslt.org/strings";
/* private namespace for this script */
ns ztp_script = "http://xml.juniper.com/ztp_script/1.0";
import '../import/junos.xsl';
/* ------------------------------------------------------------------ */
/* Constants */
/* ------------------------------------------------------------------ */
var $APPNAME = 'ztp_script[' _ $junos-context/pid _ ']';
var $SYSLOG = 'user.info';
var $TMPDIR = '/var/tmp';
var $CODEDIR = '/var/tmp';
var $ZTP_GROUP_NAME = "GR-ZTP";
var $ZTP_GROUP_STAGE_0 = "GR-ZTP-STAGE-0";
var $ZTP_GROUP_STAGE_1 = "GR-ZTP-STAGE-1";
var $ZTP_GROUP_STAGE_2 = "GR-ZTP-STAGE-2";
var $ZTP_CODE_MACRO_NAME = "CODE";
var $ZTP_CONFIG_MACRO_NAME = "CONFIG";
var $ZTP_CONFIG = "CONFIG";
var $ZTP_LOCKFILE = '/tmp/ztp_script.lock';
/* ------------------------------------------------------------------ */
/* Global variables */
/* ------------------------------------------------------------------ */
var $jnx = jcs:open();
/* ------------------------------------------------------------------ */
/* MAIN */
/* ------------------------------------------------------------------ */
match / {
    if( not( $jnx )) {
        expr jcs:syslog( $SYSLOG, $APPNAME _ ": ERROR: unable to connect to Junos API");
        expr jcs:progress( "ERROR: unable to connect to Junos API");
        expr jcs:output( $APPNAME _ ":ERROR: unable to connect to Junos API");
        terminate;
    }
    var $running = ztp_script:only_once();
    if( $running ) {
        expr jcs:syslog( $SYSLOG, $APPNAME _ ": process already running, backing off" );
        expr jcs:progress( "process already running, backing off" );
        expr jcs:output( $APPNAME _ ": process already running, backing off" );
        terminate;
    }
    expr jcs:syslog( $SYSLOG, $APPNAME _ ": SCRIPT-BEGIN" );
    expr jcs:progress( "SCRIPT-BEGIN" );
    /*----------------------------------------------------------------*/
    /*      PHASE-0: LOAD GLOBAL CSR TEMPLATE                         */
	/*----------------------------------------------------------------*/
	/* Check if configuration for the next ZTP PHASE exists */
	if ( not( ztp_script:ztp_grp_exists($ZTP_GROUP_NAME, $ZTP_CONFIG_MACRO_NAME) ) ) {
	expr jcs:progress( "ZTP-SCRIPT: Configuration file name for global template (phase 0) not specified. Stop at this point" );
	var $die = ztp_script:terminate();
	}
	var $get = <get-configuration> {
         <configuration> {
            <groups> {
               <name> $ZTP_GROUP_NAME;
            }
        }
    }
	var $got = jcs:execute( $jnx, $get );
	var $got_copy = $got;
	/* global ztp parameters for access node */
	var $ztp_config_file = $got/groups[name=$ZTP_GROUP_NAME]/apply-macro[name=$ZTP_CONFIG_MACRO_NAME]/data[name=$ZTP_CONFIG]/value;	
	/*-------------------------------------------------------*/
	/*  LOAD NEW ZTP PARAMETERS                              */
    /*-------------------------------------------------------*/
    if (not (ztp_script:load_config($ztp_config_file))) {
        expr jcs:progress( "ZTP-SCRIPT: SCRIPT FAILED TO UPDATE ZTP CONFIG. CONFIG FILE NOT FOUND OR BAD CONFIGURATION");
	    var $die = ztp_script:terminate();
    } else {
        expr jcs:progress( "ZTP-SCRIPT: ZTP CONFIG SUCCESSFULLY UPDATED" );
	}
    /*----------------------------------------------------------------*/
	/*  Deactivate configuration group for PHASE-0                    */
	/*  Activate   configuration group for PHASE-1 if exists          */	
	/*----------------------------------------------------------------*/	
	var $options_s0 := {
        <commit-options> {
            <log> "Enabling ZTP PHASE-1"; 
        }
	}
    var $change_s0 = {
        <configuration> {
            <apply-groups delete = "delete"> $ZTP_GROUP_STAGE_0;
			if ( ztp_script:ztp_grp_exists($ZTP_GROUP_STAGE_1) ) {
				<apply-groups> $ZTP_GROUP_STAGE_1;
			} else {
				expr jcs:progress( "ZTP-SCRIPT: Group name " _$ZTP_GROUP_STAGE_1 _ " with PHASE-1 script name and location NOT FOUND in global template. STOP ZTP AT THIS POINT" );
				var $die = ztp_script:terminate();
			}
        }
    }
    /* Load configuration */	
    var $results_stage_0 := { call jcs:load-configuration( $action="merge", $commit-options=$options_s0, $configuration=$change_s0, $connection = $jnx ); }
    if ($results_stage_0//xnm:warning) {
        for-each ($results_stage_0//xnm:warning) {
	        expr jcs:progress( "ZTP-SCRIPT: commit warning: " _ message );
        }
    }
	if ($results_stage_0//xnm:error) {
        for-each ($results_stage_0//xnm:error) {
            expr jcs:progress( "ZTP-SCRIPT: commit error: " _ message );
        }
	    expr jcs:progress( "ZTP-SCRIPT: ZTP PHACE 0 Script failed." );
		var $die = ztp_script:terminate();
    } else {
        expr jcs:progress( "ZTP-SCRIPT: ZTP PHASE 0 COMPLITED SUCCESSFULLY" );
	}
    var $die = ztp_script:terminate();
}
/* ------------------------------------------------------------------ */
/*   FUNCYION load_config LOADS CONFIGURATION FROM FILE               */
/* ------------------------------------------------------------------ */
function ztp_script:load_config($config_url, $action = "merge") {
	mvar $load_config = true();
	var $phrase = {
		if ($action == "merge") {
			expr "merged to existed configuration";
		} else {
			if ($action == "replace") {
				expr " and replace existed statements";
			} else {
				expr " be merged (default action) to existed configuration";
			}
		}
	}
	var $new_action = {
		if (($action == "merge") or ($action == "replace")) {
			expr $action;
		} else {
            expr "merge";
		}
	}
    expr jcs:progress( "Config file: " _ $config_url _ " will be loaded and " _ $phrase  );
    /* lock the config */
    var $lock = <lock-configuration>;
    var $did_lock = jcs:execute( $jnx, $lock );
    if ( $did_lock//self::xnm:error ) {
        expr jcs:syslog( $SYSLOG, $APPNAME _ ": ERROR: unable to lock config" );
        expr jcs:progress( "ERROR: unable to lock config" );
        set $load_config = false();
    } else {
        expr jcs:progress( "Got config lock" );
    }
    if ($load_config) {       
	    set $load_config = false();
        expr jcs:progress( "Processing config url " _ $config_url );
        expr jcs:syslog( $SYSLOG, $APPNAME _ ": Processing config url " _ $config_url );
        /* load in new config */
        var $do_load = <load-configuration action="merge" url=$config_url format="text">;
        var $did_load = jcs:execute( $jnx, $do_load );
        if( not( $did_load/load-success )) {
            expr jcs:syslog( $SYSLOG, $APPNAME _ ": ERROR: unable to load config " _ $config_url );
            expr jcs:progress( "ERROR: unable to load config " _ $config_url );
        } else {
            expr jcs:progress( "CONFIG LOAD SUCCESS" );
            var $commit = <commit-configuration> {
                <full>;
                <synchronize>;
                <force-synchronize>;
                <log> "Initial config load";
            }
            var $did_commit = jcs:execute( $jnx, $commit );
            if ( $did_commit//self::xnm:error ) {
                expr jcs:progress( "COMMIT FAILED" );
                expr jcs:syslog( $SYSLOG, $APPNAME _ ": COMMIT FAILED" );
            } else {
                expr jcs:progress( "COMMIT SUCCESS" );
                /*
                * reboot here
                * var $reboot := jcs:execute( $jnx, 'request-reboot' );
                */
                var $unlock = <unlock-configuration>;
                var $did_unlock = jcs:execute( $jnx, $unlock );
                expr jcs:progress( "release config lock" );
				set $load_config = true();
                /*var $die = ztp_script:terminate();*/
            }
        }
    } /* end of foreach config_url */
	if (not($load_config)) {
		/* if we make it here, we failed to load the config */
        var $unlock = <unlock-configuration>;
        var $did_unlock = jcs:execute( $jnx, $unlock );
        expr jcs:progress( "release config lock" );
	}
    result $load_config;	
}
/* --------------------------------------------------------------------------------- */
/* FUNCTION ztp_grp_exists VERIFIES IF  configuration group name and apply-macro name*/
/* EXIST IN THE CONFIGURATION and RETURNES True or False 
/* --------------------------------------------------------------------------------- */
function ztp_script:ztp_grp_exists($group_name, $macro_name = "N/A") {
	var $get_grp = <get-configuration> {
         <configuration> {
            <groups> {
				<name>;
			}
	    }
    }
	/* geting variables from apply-macro */
	var $got_grp = jcs:execute( $jnx, $get_grp );
	mvar $grp_flag = "skip";
	for-each ($got_grp/groups/name) {
		set $grp_flag = {
			if  ((../name!=$group_name) and ($grp_flag!="exists")) {
			    expr "skip";
			} else {
				expr "exists";
			} /* End if */
		}
    }
	/* Verification for the apply-macro */	
    if ($grp_flag != "exists") {
		expr jcs:progress( "ZTP-SCRIPT: Can't find a group " _ $group_name);
        result false();
	} else {
		expr jcs:progress( "ZTP-SCRIPT: Found a group " _ $group_name);
		if ( $macro_name!="N/A") {
	        expr jcs:progress( "ZTP-SCRIPT: Looking for apply-macro " _ $macro_name _ " in group " _ $group_name );
	        /* Look for apply-macro with specified name */
	        var $get_mcr = <get-configuration> {
                <configuration> {
                    <groups> {
				        <name> $group_name;
			        }
	            }
            }
	        /* getting variables from apply-macro */
	        var $got_mcr = jcs:execute( $jnx, $get_mcr );
	        mvar $mcr_flag = "skip";
	        for-each ($got_mcr/groups[name=$group_name]/apply-macro/name) {
		        set $mcr_flag = {
			        if  ((../name!=$macro_name) and ($mcr_flag!="exists")) {
			            expr "skip";
			        } else {
				        expr "exists";
			        } /* End if */
		        }
            }
	        /* Verification for the group */	
            if ($mcr_flag != "exists") {
		        expr jcs:progress( "ZTP-SCRIPT: Can't find apply-macro " _ $macro_name );
		        result false();
	        } else {
		        expr jcs:progress( "ZTP-SCRIPT: Found apply-macro " _ $macro_name );
			    result true();
	        }		
	    } else {
			result true();
		}
	}	
}	
/* ------------------------------------------------------------------ */
/* Helper routines
/* ------------------------------------------------------------------ */
function ztp_script:file-exists( $filename ) {
    var $ls_file = <file-list> { <path> $filename; }
    var $ls_got = jcs:execute( $jnx, $ls_file );
    var $retval = boolean( $ls_got//file-information );
    result $retval;
}
function ztp_script:file-delete( $filename ) {
    var $do_rm = <file-delete> { <path> $filename; }
    var $did_rm = jcs:execute( $jnx, $do_rm );
    /* @@@ trap error */
    result true();
}
function ztp_script:only_once() {
    if( ztp_script:file-exists( $ZTP_LOCKFILE )) {
        result true();
    } else {
        var $do_lock = <file-put> {
             <filename> $ZTP_LOCKFILE;
             <encoding> 'ascii';
             <file-contents> 'locked';
        }
    var $did_lock = jcs:execute( $jnx, $do_lock );
    result false();
    }
}
function ztp_script:terminate() {
    expr jcs:syslog( $SYSLOG, $APPNAME _ ": SCRIPT-TERMINATE" );
    var $rm_lock = ztp_script:file-delete( $ZTP_LOCKFILE );
    terminate;
}
