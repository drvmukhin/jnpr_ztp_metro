version 1.1;
/* ------------------------------------------------------------------ */
/* This script performs PHASE-1 of the zero touch provisioning for    */
/* the ACX platform. Script used for provisioning of the Layer 2 oam  */
/* plane which enables management access to the access node on its    */
/* interface. At the end of PHASE-1 Script will enable configuration  */
/* for the following elements:                                        */
/* 1. IRB Interface                                                   */
/* 2. Static IP configuration copied from Dynamic IP settings of the  */
/*    DHCP Client                                                     */
/* 3. OAM VLAN                                                        */
/* 4. OAM Bridge Domain                                               */
/* 5. Core facing interfaces (NNI)                                    */
/* 6. Generates a unique host name of the node                        */
/* 7. Deletes DHCP client configuration                               */
/* 8. Enables Phase 2 of the ZTP process                              */
/* -------------------------------------------------------------------*/
/* Version 1.1 Vasily Mukhin vmukhin@juniper.net                      */
/* Based on jctyztp script by Jeremy Schulman and Brian Sherwood      */
/* ------------------------------------------------------------------ */
/* XML namespaces*/
/* Juniper */
ns junos = "http://xml.juniper.net/junos/*/junos";
ns xnm = "http://xml.juniper.net/xnm/1.1/xnm";
ns jcs = "http://xml.juniper.net/junos/commit-scripts/1.0";
ns exsl extension = "http://exslt.org/common";
/* EXSLT */
ns str extension = "http://exslt.org/strings";
/* private namespace for this script */
ns ztp_script = "http://xml.juniper.com/ztp_script/1.0";
import '../import/junos.xsl';
/* ------------------------------------------------------------------ */
/* Constants                                                          */
/* ------------------------------------------------------------------ */
var $APPNAME = 'ztp_script[' _ $junos-context/pid _ ']';
var $JNPR_ZTP_SCRIPT = "ZTP Phase 1 SCRIPT";
var $SYSLOG = 'user.info';
var $TMPDIR = '/var/tmp';
var $CODEDIR = '/var/tmp';
var $ZTP_GROUP_PLATFORM = "GR-ZTP-PLATFORM";
var $ZTP_GROUP_BOX = "GR-ZTP-BOX";
var $ZTP_CODE_MACRO_NAME = "CODE";
var $ZTP_CONFIG_MACRO_NAME = "CONFIG";
var $ZTP_MACRO_NAME_PREFIX = "ZTP-";
var $ZTP_GROUP_NNI_TAG = "GR-NNI-TAG";
var $ZTP_GROUP_AGG_NNI_TAG = "GR-AGG-INTF-TAG";
var $ZTP_GROUP_STAGE_1 = "GR-ZTP-STAGE-1";
var $ZTP_GROUP_STAGE_2 = "GR-ZTP-STAGE-2";
var $ZTP_HOSTNAME = "HOST_NAME";
var $ZTP_BD_OAM = "BD-ZTP-OAM";
var $ZTP_BD_BOOT = "BD-ZTP-BOOTP";
var $ZTP_OAM_VLAN = "OAM_VLAN";
var $ZTP_LOCKFILE = '/tmp/ztp_script.lock';
var $ZTP_IP_VLAN = "1";
/* ------------------------------------------------------------------ */
/* Global variables                                                   */
/* ------------------------------------------------------------------ */
var $jnx = jcs:open();
/* ------------------------------------------------------------------ */
/* MAIN                                                               */
/* ------------------------------------------------------------------ */
match / {
    if( not( $jnx )) {
        expr jcs:syslog( $SYSLOG, $APPNAME _ ": ERROR: unable to connect to Junos API");
        expr jcs:progress( $JNPR_ZTP_SCRIPT _ ": ERROR: unable to connect to Junos API");
        expr jcs:output( $JNPR_ZTP_SCRIPT _ ": ERROR: unable to connect to Junos API");
        terminate;
    }
    var $running = ztp_script:only_once();
    if( $running ) {
        expr jcs:syslog( $SYSLOG, $APPNAME _ ": process already running, backing off" );
        expr jcs:progress( $JNPR_ZTP_SCRIPT _ ": process already running, backing off" );
        expr jcs:output( $JNPR_ZTP_SCRIPT _ ": process already running, backing off" );
        terminate;
    }
    expr jcs:syslog( $SYSLOG, $APPNAME _ ": ZTP Phase 1 BEGIN" );
    expr jcs:progress( $JNPR_ZTP_SCRIPT _ ": ZTP Phase 1 BEGIN" );
    /*--------------------------------------------------------------*/
    /*  DEACTIVATE PHASE-1 Configuration group                      */
    /*--------------------------------------------------------------*/
    expr jcs:progress( $JNPR_ZTP_SCRIPT _ ": Deactivating event-options for ZTP Phase 1 script..." );
    var $options_0 := {
        <commit-options> {
            <log> "Deactivating configuration group for Phase 1"; 
        }
    }
    var $change_0 = {
        <configuration> {
            <apply-groups delete="delete"> $ZTP_GROUP_STAGE_1;
        }
    }
    /* Load new configuration */    
    var $results_stage_0 := { call jcs:load-configuration( $action="merge", $commit-options=$options_0, $configuration=$change_0, $connection = $jnx ); }
    if ($results_stage_0//xnm:warning) {
        for-each ($results_stage_0//xnm:warning) {
            expr jcs:progress( $JNPR_ZTP_SCRIPT _ ": commit warning: " _ message );
        }
    }
    if ($results_stage_0//xnm:error) {
        for-each ($results_stage_0//xnm:error) {
            expr jcs:progress( $JNPR_ZTP_SCRIPT _ ": commit error: " _ message );
        }
        expr jcs:progress( $JNPR_ZTP_SCRIPT _ ": Commit failed." );
        var $die = ztp_script:terminate();
    } else {
        expr jcs:progress( $JNPR_ZTP_SCRIPT _ ": Configuration for event-options for ZTP Phase 1 was disabled" );
    }
    mvar $nRollback = 1;
    /*---------------------------------------------------------------------------------------*/
    /* Verify that configuration group and apply-macro with platform specific ZTP parameters */
    /* exist in the global configuration template                                            */
    /*---------------------------------------------------------------------------------------*/
    var $serial_no = ztp_script:get_serial_number();
    var $platform = ztp_script:get_platform();
    expr jcs:progress( $JNPR_ZTP_SCRIPT _ ": Looking for " _ $platform _ " specific parameters." );
    expr jcs:syslog( $SYSLOG, $APPNAME _ "Looking for " _ $platform _ " specific parameters." );
    if ( not( ztp_script:ztp_grp_exists($ZTP_GROUP_PLATFORM, $ZTP_MACRO_NAME_PREFIX _ $platform) ) ) {
    expr jcs:progress( $JNPR_ZTP_SCRIPT _ ": ERROR: Failed to find platform specific ZTP parameters in global template." );
    expr jcs:progress( $JNPR_ZTP_SCRIPT _ ": Note: Make sure that apply-macro " _ $ZTP_MACRO_NAME_PREFIX _ $platform _ " exists under " _ $ZTP_GROUP_PLATFORM _ " group in global template." );
    var $ztd_rollback = ztp_script:ztp_rollback_phase0();
	expr jcs:progress( $JNPR_ZTP_SCRIPT _ ": Script terminated." );
    var $die = ztp_script:terminate();
    }
    
    /* Creating static configuration and enabling management access to the node */
    /* 
        0. Read parameters assigned dynamically via DHCP:
           - ip address and mask a.b.c.d/n
           - default route         
        1. Create host-name: csr<c.d>-<platform>)
        2. Creates OAM VLAN on NNIs:
            - NNIs are listed under apply-macros (platform specific list)
        3. Creates IRB interfaces
            - Copies address assigned by DHCP server to IRB
        4. Deletes DHCP-client configuration
        5. Creates OAM Bridge-domain and placess following interfaces into it
            - NNI IFLs
            - IRB
        6. Enables VSTP and LLDP protocols on NNI interfaces
    */
    var $results_step_1 = ztp_script:set_dhcp_to_static($platform); 
    if ($results_step_1//xnm:warning) {
        for-each ($results_step_1//xnm:warning) {
            expr jcs:progress( $JNPR_ZTP_SCRIPT _ ": set_dhcp_to_static() commit warning: " _ message );
        }
    }
    if ($results_step_1//xnm:error) {
        for-each ($results_step_1//xnm:error) {
            expr jcs:progress( $JNPR_ZTP_SCRIPT _ ": dhcp_to_static() commit error: " _ message );
        }
        expr jcs:progress( $JNPR_ZTP_SCRIPT _ " ERROR: Commit failed." );
		expr jcs:progress( $JNPR_ZTP_SCRIPT _ ": Note: Verify parametrs in global conviguration template." );		
		var $ztd_rollback = ztp_script:ztp_rollback_phase0();
		expr jcs:progress( $JNPR_ZTP_SCRIPT _ ": Script terminated." );		
		var $die = ztp_script:terminate();
    } else {
        expr jcs:progress( $JNPR_ZTP_SCRIPT _ ": Platform specific configuration committed successfully." );
        set $nRollback = $nRollback + 1;
    }
    /*-----------------------------------------*/
    /* Looking for box specific ZTP parameters */
    /*-----------------------------------------*/
    expr jcs:progress( $JNPR_ZTP_SCRIPT _ ": Looking for box (S/N: " _ $serial_no _ ") specific parameters." );
    expr jcs:syslog( $SYSLOG, $APPNAME _ "Looking for box (S/N: " _ $serial_no _ ") specific parameters." );
    if ( not( ztp_script:ztp_grp_exists($ZTP_GROUP_BOX, $serial_no) ) ) {
        expr jcs:progress( $JNPR_ZTP_SCRIPT _ ": Box specific configuration for S/N: " _ $serial_no _ " was not found in global template. Skip this step." );
    }
    else {
        /* Configuring box specific parameters (fxp0 and host name) */
        var $results_step_2 = ztp_script:set_box_specific_config($platform, $serial_no);
        if ($results_step_2//xnm:warning) {
            for-each ($results//xnm:warning) {
                expr jcs:progress( $JNPR_ZTP_SCRIPT _ ": set_box_specific_config() commit warning: " _ message );
            }
        }
        if ($results_step_2//xnm:error) {
            for-each ($results_step_1//xnm:error) {
                expr jcs:progress( $JNPR_ZTP_SCRIPT _ ": set_box_specific_config() commit error: " _ message );
            }
            expr jcs:progress( $JNPR_ZTP_SCRIPT _ ": ERROR: Failed to commit box specific parameters." );
            expr jcs:progress( $JNPR_ZTP_SCRIPT _ ": Skip this step and continue ZTP process." );
            var $ztd_rollback = ztp_script:rollback_cfg(0);            
            /* var $die = ztp_script:terminate(); */
        } else {
            expr jcs:progress( $JNPR_ZTP_SCRIPT _ ": Box specific configuration committed successfully." );
            set $nRollback = $nRollback + 1;
        }
    }
    /*--------------------------------------------------------------*/
    /*  DELETE Box specific configuration if exists                 */
    /*  ACTIVATE   PHASE 2 Configuration group if exists            */  
    /*--------------------------------------------------------------*/
    var $options_1 := {
        <commit-options> {
            <log> "Enabling next ZTP Phase"; 
        }
    }
    var $change_1 = {
        <configuration> {
            if ( ztp_script:ztp_grp_exists($ZTP_GROUP_BOX) ) {
                <groups delete="delete"> {
                    <name> $ZTP_GROUP_BOX;
                    expr jcs:syslog( $SYSLOG, $APPNAME _ ": Deleting configuration group " _ $ZTP_GROUP_BOX );
                    expr jcs:progress( $JNPR_ZTP_SCRIPT _ ": Deleting configuration group " _ $ZTP_GROUP_BOX );
                }
            }
            if ( ztp_script:ztp_grp_exists($ZTP_GROUP_STAGE_2) )  {
                <apply-groups> $ZTP_GROUP_STAGE_2;
            } else {
                expr jcs:syslog( $SYSLOG, $APPNAME _ ": WARNING: No configuration group for Phase 2 found in global template. STOP at this point.");
                expr jcs:progress( $JNPR_ZTP_SCRIPT _ ": WARNING: No configuration group for Phase 2 found in global template. STOP at this point.");
            } 
        }
    }
    /* Load new configuration */    
    var $results_stage_1 := { call jcs:load-configuration( $action="merge", $commit-options=$options_1, $configuration=$change_1, $connection = $jnx ); }
    if ($results_stage_1//xnm:warning) {
        for-each ($results_stage_1//xnm:warning) {
            expr jcs:progress( $JNPR_ZTP_SCRIPT _ ": commit warning: " _ message );
        }
    }
    if ($results_stage_1//xnm:error) {
        for-each ($results_stage_1//xnm:error) {
            expr jcs:progress( $JNPR_ZTP_SCRIPT _ ": commit error: " _ message );
        }
        expr jcs:progress( $JNPR_ZTP_SCRIPT _ ": Commit failed. " );
        expr jcs:progress( $JNPR_ZTP_SCRIPT _ ": Rollback to beginning of phase 1 and exiting ztp_script now." );
        var $ztd_rollback = ztp_script:rollback_cfg($nRollback);            
        var $die = ztp_script:terminate();
    } else {
        expr jcs:progress( $JNPR_ZTP_SCRIPT _ ": Configuration was committed" );
    }
    expr jcs:progress( $JNPR_ZTP_SCRIPT _ ": ZTP Phase 1 completed successfully" );
    var $die = ztp_script:terminate();
}
/* -------------------------------------------------------------------------------- */
/* FUNCTION ztp_grp_exists VERIFIES IF  configuration group name and apply-macro    */
/* name EXIST IN THE CONFIGURATION and RETURNES True or False                       */
/* -------------------------------------------------------------------------------- */
function ztp_script:ztp_grp_exists($group_name, $macro_name = "N/A") {
    var $get_grp = <get-configuration> {
         <configuration> {
            <groups> {
                <name>;
            }
        }
    }
    /* getting variables from apply-macro */
    var $got_grp = jcs:execute( $jnx, $get_grp );
    mvar $grp_flag = "skip";
    for-each ($got_grp/groups/name) {
        set $grp_flag = {
            if  ((../name!=$group_name) and ($grp_flag!="exists")) {
                expr "skip";
            } else {
                expr "exists";
            } /* End if */
        }
    }
    /* Verification for the apply-macro */  
    if ($grp_flag != "exists") {
        expr jcs:progress( $JNPR_ZTP_SCRIPT _ ": Can't find a group " _ $group_name);
        result false();
    } else {
        expr jcs:progress( $JNPR_ZTP_SCRIPT _ ": Found a group " _ $group_name);
        if ( $macro_name!="N/A") {
            expr jcs:progress( $JNPR_ZTP_SCRIPT _ ": Looking for apply-macro " _ $macro_name _ " in group " _ $group_name );
            /* Look for apply-macro with specified name */
            var $get_mcr = <get-configuration> {
                <configuration> {
                    <groups> {
                        <name> $group_name;
                    }
                }
            }
            /* geting variables from apply-macro */
            var $got_mcr = jcs:execute( $jnx, $get_mcr );
            mvar $mcr_flag = "skip";
            for-each ($got_mcr/groups[name=$group_name]/apply-macro/name) {
                set $mcr_flag = {
                    if  ((../name!=$macro_name) and ($mcr_flag!="exists")) {
                        expr "skip";
                    } else {
                        expr "exists";
                    } /* End if */
                }
            }
            /* Verification for the group */    
            if ($mcr_flag != "exists") {
                expr jcs:progress( $JNPR_ZTP_SCRIPT _ ": Can't find apply-macro " _ $macro_name );
                result false();
            } else {
                expr jcs:progress( $JNPR_ZTP_SCRIPT _ ": Found apply-macro " _ $macro_name );
                result true();
            }       
        } else {
            result true();
        }
    }   
}   
/* ------------------------------------------------------------------ */
/* Function: remove_old_cfg - DELETES CONFIGURATION GROUPS            */
/* ------------------------------------------------------------------ */
function ztp_script:remove_old_cfg($group_name) {
    expr jcs:progress( $JNPR_ZTP_SCRIPT _ ": Removing old configuration group: " _ $group_name );
    expr jcs:syslog( $SYSLOG, $APPNAME _ "Removing old configuration group: " _ $group_name );
    var $get = <get-configuration> {
         <configuration> {
            <groups> {
                <name>;
            }
        }
    }
    /* looking for the given group-name */
    var $got = jcs:execute( $jnx, $get );
    mvar $delete_flag = "skip";
    for-each ($got/groups/name) {
        set $delete_flag = {
            if  ((../name!=$group_name) and ($delete_flag!="delete")) {
                expr "skip";
            } else {
                expr "delete";
            } /* End if */
        }
    }
    var $options := {
         <commit-options> {
            <log> "deleting configuration group " _ $group_name;; 
        }
    }
    var $change = {
        <configuration> {
            <groups delete="delete"> {
                <name> $group_name;
            }
        }
    }
    /* Deleting config for the group */ 
        if ($delete_flag == "delete") {
            var $results := { call jcs:load-configuration($action="merge", $commit-options=$options, $configuration=$change, $connection = $jnx ); }
            expr jcs:progress( $JNPR_ZTP_SCRIPT _ ": group " _ $group_name _ " was deleted" );
        } else {
            expr jcs:progress( $JNPR_ZTP_SCRIPT _ ": Group: " _ $group_name _ " was not found. Nothing to delete ");
        }
}
/* ------------------------------------------------------------------ */
/* Function: set_dhcp_to_static - GENERATES NEW STATIC CONFIGURATION  */
/* ------------------------------------------------------------------ */
function ztp_script:set_dhcp_to_static($platform) {
    expr jcs:progress( $JNPR_ZTP_SCRIPT _ ": Retrieve DHCP client bindings" );
    expr jcs:syslog( $SYSLOG, $APPNAME _ ": Retrieve DHCP client bindings" );
     /* GET ZTP VLAN-ID */
    var $get = <get-configuration> {
         <configuration> {
            <groups> {
               <name> $ZTP_GROUP_PLATFORM;
               <apply-macro> {
                  <name> $ZTP_MACRO_NAME_PREFIX _ $platform;
                }
            }
        }
    }
    /* getting variables from apply-macro */
    var $got = jcs:execute( $jnx, $get );
    mvar $ztp_oam_vlan = "2";
    mvar $ztp_host_prefix = "csr";  
    for-each ($got/groups[name=$ZTP_GROUP_PLATFORM]/apply-macro[name=$ZTP_MACRO_NAME_PREFIX _ $platform]/data/name) {
        if (../name == $ZTP_OAM_VLAN ) {
            set $ztp_oam_vlan = ../value;
            expr jcs:progress( $JNPR_ZTP_SCRIPT _ ": FOUND VLAN: " _ ../value );
        }
        if (../name == $ZTP_HOSTNAME ) {
            set $ztp_host_prefix = ../value;
            expr jcs:progress( $JNPR_ZTP_SCRIPT _ ": FOUND HOST PREFIX: " _ ../value );
        }   
    }
     /* GET VALUES OF THE DHCP OPTIONS */
    var $get_dhcp_detail = <get-dhcp-client-binding-information> {
                               <detail>;
                           }
    var $dhcp_bind := jcs:execute( $jnx, $get_dhcp_detail );
    /* - GET ZTP INTERFACE - */ 
    var $ztp_interface = $dhcp_bind/dhcp-binding/interface-name;
    expr jcs:progress( $JNPR_ZTP_SCRIPT _ ": DHCP client runs on ZTP interface: " _ $ztp_interface );
    /* - GET ROUTER IP ADDRESS - */ 
    var $ztp_router = $dhcp_bind/dhcp-binding/dhcp-option-table/dhcp-option[dhcp-option-name="router"]/dhcp-option-value;
    expr jcs:progress( $JNPR_ZTP_SCRIPT _ ": Found DHCP Option for router: " _ $ztp_router ); 
    var $post_str = {
        if ( contains($ztp_router, '[ ')) {
            expr substring-after($ztp_router, '[ ');
        } else {
            expr $ztp_router;
        }
    }
    /* - GET NEXT-HOP FOR DEFAULT ROUTE - */
    var $ztp_next_hop = {
        if ( contains($post_str, ' ')) {
            expr substring-before($post_str, ' ');
        } else {
            expr $post_str;
        }
    }
    expr jcs:progress( $JNPR_ZTP_SCRIPT _ ": Derived next-hop: " _ $ztp_next_hop );
    /* Looling for real ip-address assigned to dhcp interface */    
    var $get_interface_terse = <get-interface-information> {
                               <terse>;
                               <interface-name> $ztp_interface;
                           }
    var $interface_info := jcs:execute( $jnx, $get_interface_terse );
    /* - GET DHCP IP ADDRESS - */
    var $irb_ip_address = $interface_info/logical-interface/address-family[address-family-name="inet"]/interface-address/ifa-local;
    expr jcs:progress( $JNPR_ZTP_SCRIPT _ ": DHCP ip address found: " _ $irb_ip_address);
    /* Create configuration with static IP */
    var $options := {
         <commit-options> {
            <log> "setting irb interface"; 
        }
    }
    /* - CREATE CONFIGURATION - */
    /*
        - Deletes DHCP-client configuration
        - Creates OAM Bridge-domain
        - Configures OAM vlan-bridge unit on ZTP interface
        - Moves OAM unit into bridge domain
        - Creates irb
        - Copies DHCP-client IP-address to static irb interface
    */
    var $ztp_device = substring-before($ztp_interface, '.');
    var $ztp_device_unit = substring-after($ztp_interface,'.');
    expr jcs:progress( $JNPR_ZTP_SCRIPT _ ": Parsing ZTP Interface: ifd=" _ $ztp_device _ " unit=" _ $ztp_device_unit );
    var $irb_unit_num = $ztp_oam_vlan;
    var $ip_3_4 = substring-after(substring-after(substring-before($irb_ip_address,'/'),'.'),'.');  
    var $change = {
        <configuration> {
            <interfaces> {
                <interface> {
                    call emit-deactivate-dhcp-client($platform, $ztp_device, $ztp_device_unit);
                }
                <interface> {
                    <name> $ztp_device;
                    <flexible-vlan-tagging>;
                    <encapsulation> "flexible-ethernet-services";
                    <unit> {
                        <name> $ztp_oam_vlan;
                        <encapsulation> "vlan-bridge";
                        <vlan-id> $ztp_oam_vlan;
                    }
                }
                <interface> {
                    <name> "irb";
                    <unit> {
                        <name> $irb_unit_num;
                        <family> {
                            <inet> {
                                <address> $irb_ip_address;
                            } 
                        }
                    }
                }
                call create-nni-interfaces($platform, $ztp_oam_vlan, $got);
            }
            call emit-bridge-domain($platform, $irb_unit_num, $ztp_device, $ztp_oam_vlan, $got);
            <routing-options> {
                <static> {
                    <route> {
                        <name> "0.0.0.0/0";
                        <next-hop> $ztp_next_hop;
                    }
                }
            }
            <protocols> {
                <vstp> {
                    for-each ($got/groups[name=$ZTP_GROUP_PLATFORM]/apply-macro[name=$ZTP_MACRO_NAME_PREFIX _ $platform]/data/name) {
                        if (contains(../name,"NNI" )) {
                            <interface> {
                                <name> ../value;
                            }
                            <vlan> {
                                <name> $ztp_oam_vlan;                       
                            }
                        }
                    }
                }
                <lldp> {
                    for-each ($got/groups[name=$ZTP_GROUP_PLATFORM]/apply-macro[name=$ZTP_MACRO_NAME_PREFIX _ $platform]/data/name) {
                        if (contains(../name,"NNI" )) {
                            <interface> {
                                <name> ../value;
                            } 
                        }
                    }
                }
            }
            call create-re0-group($platform, $ip_3_4, $ztp_host_prefix);
        }
    }
    /* Load configuration for group re0 */  
    var $results := { call jcs:load-configuration( $action="merge", $commit-options=$options, $configuration=$change, $connection = $jnx ); } 
    result $results;
}
/*--------------------------------------------------------------------*/
/*  Template: create-re0-group - CREATES re0 group and a host-name    */
/*--------------------------------------------------------------------*/
template create-re0-group($platform, $ip_3_4, $ztp_host_prefix) {
    if (($platform=="acx5096") or ($platform=="acx5048")) { 
        <groups> {
            <name> "member0";
            <system> {
                <host-name> $ztp_host_prefix _ $ip_3_4 _ "-" _ $platform;
            }
        }
        <apply-groups> "member0";
    } else {
        <groups> {
            <name> "re0";
            <system> {
                <host-name> $ztp_host_prefix _ $ip_3_4 _ "-" _ $platform;
            }
        }
        <apply-groups> "re0";
    }   
}
/* -------------------------------------------------------------------- */
/*  Template: create-nni-interfaces - CONFIGURES CORE-FACING INTERFACES */
/* -------------------------------------------------------------------- */
template create-nni-interfaces($platform, $ztp_oam_vlan, $got) {
    for-each ($got/groups[name=$ZTP_GROUP_PLATFORM]/apply-macro[name=$ZTP_MACRO_NAME_PREFIX _ $platform]/data/name) {
        if ( contains(../name,"NNI" )){
            expr jcs:progress( $JNPR_ZTP_SCRIPT _ ": Configuring NNI interface: " _ ../value);
            <interface> {
                <name> ../value;
                <flexible-vlan-tagging>;
                <encapsulation> "flexible-ethernet-services";
                <apply-groups> $ZTP_GROUP_NNI_TAG;
                <native-vlan-id> $ztp_oam_vlan;
                <unit> {
                    <name> $ztp_oam_vlan;
                    <description> "OAM VLAN ENABLES ZTP AND NMS ACCESS";
                    <encapsulation> "vlan-bridge";
                    <vlan-id> $ztp_oam_vlan;
                }
            }
        }
    }
}
/*---------------------------------------------------------------------- */
/* Template: emit-nni-interfaces - ADDS INTERFACES TO OSPF CONFIGURATION */
/*---------------------------------------------------------------------- */
template emit-nni-interfaces($platform, $got) {
    expr jcs:progress( $JNPR_ZTP_SCRIPT _ ": Configuring OSPF Interfaces ");     
    for-each ($got/groups[name=$ZTP_GROUP_PLATFORM]/apply-macro[name=$ZTP_MACRO_NAME_PREFIX _ $platform]/data/name) {
        if (contains(../name,"NNI" )) {
            <interface> {
                <name> ../value _ ".0";
            } 
        }
    }
}
/*----------------------------------------------------------------- */
/*  Template: emit-deactivate-dhcp-client DEACTIVATE DHCP CLIENT    */
/*----------------------------------------------------------------- */
template emit-deactivate-dhcp-client($platform, $ztp_device, $ztp_device_unit) {
    if (($platform=="acx5096") or ($platform=="acx5048")) { 
        <name> $ztp_device;
        <flexible-vlan-tagging>;
        <encapsulation> "flexible-ethernet-services";
        <unit> {
            <name> $ztp_device_unit;
            <vlan-id> $ZTP_IP_VLAN;
            <family> {
                <inet> {
                    <dhcp inactive="inactive">;
                }                               
            }
        }
    } else {
        <name> $ztp_device;
        <flexible-vlan-tagging>;
        <encapsulation> "flexible-ethernet-services";
        <unit> {
            <name> $ztp_device_unit;
            <vlan-id> $ZTP_IP_VLAN;
            <family> {
                <inet> {
                    <dhcp-client inactive="inactive">;
                }                               
            }
        }
    }
}
/*--------------------------------------------------------------------*/
/*  Template: emit-bridge-domain - CONFIGURES BRIDGE DOMAIN WITH IRB  */
/*--------------------------------------------------------------------*/
template emit-bridge-domain($platform, $irb_unit_num, $ztp_device, $ztp_oam_vlan, $got) {
    if (($platform=="acx5096") or ($platform=="acx5048")) {
        expr jcs:progress( $JNPR_ZTP_SCRIPT _ ": Configuring bridge-domain (vlans): " _ $ZTP_BD_OAM);
        <vlans> {
            <vlan> {
                <name> $ZTP_BD_OAM;
                <vlan-id> $ztp_oam_vlan;
                <l3-interface> "irb." _ $irb_unit_num; 
                <interface> $ztp_device _ "." _ $ztp_oam_vlan;
                for-each ($got/groups[name=$ZTP_GROUP_PLATFORM]/apply-macro[name=$ZTP_MACRO_NAME_PREFIX _ $platform]/data/name) {
                    if (contains(../name,"NNI" )) {
                        <interface> ../value _ "." _ $ztp_oam_vlan;
                    }
                }
            }
        }
    } else {
        expr jcs:progress( $JNPR_ZTP_SCRIPT _ ": Configuring bridge-domain: " _ $ZTP_BD_OAM);        
        <bridge-domains> {
            <domain> {
                <name> $ZTP_BD_OAM;
                <vlan-id> $ztp_oam_vlan;
                <routing-interface> "irb." _ $irb_unit_num; 
                <interface> $ztp_device _ "." _ $ztp_oam_vlan;
                for-each ($got/groups[name=$ZTP_GROUP_PLATFORM]/apply-macro[name=$ZTP_MACRO_NAME_PREFIX _ $platform]/data/name) {
                    if (contains(../name,"NNI" )) {
                        <interface> ../value _ "." _ $ztp_oam_vlan;
                    }
                }
            }
        }
    }
}
/* ------------------------------------------------------------------ */
/* Function: get_platform - RETURNS PLATFORM TYPE                     */
/* ------------------------------------------------------------------ */
function ztp_script:get_platform() {
    expr jcs:progress( $JNPR_ZTP_SCRIPT _ ": Retrieve type of platform" );
    expr jcs:syslog( $SYSLOG, $APPNAME _ ": Retrieve Platform Type" );
     /* get platform */
    var $chassis_software := jcs:execute( $jnx, 'get-software-information' );
    var $ztp_platform = {
        if ( $chassis_software//product-model) {
            expr $chassis_software//product-model;
        } else {
            expr $chassis_software/software-information/product-model;
        }
    }
    expr jcs:progress( $JNPR_ZTP_SCRIPT _ ": platform = " _ $ztp_platform );
    expr jcs:syslog( $SYSLOG, $APPNAME _ ": platform = " _ $ztp_platform );
    result $ztp_platform;
}
/* -------------------------------------------------------------------- */
/* Function: get_serial_number - RETURNES SERIAL NUMBER OF THE NODE     */
/* -------------------------------------------------------------------- */
function ztp_script:get_serial_number() {
     expr jcs:progress( $JNPR_ZTP_SCRIPT _ ": Getting box Serial Number" );
     expr jcs:syslog( $SYSLOG, $APPNAME _ ": Getting box Serial Number" );
     /* get our serial number */
    var $chassis_hardware := jcs:execute( $jnx, 'get-chassis-inventory' );
    var $serial_no = $chassis_hardware/chassis/serial-number;
    expr jcs:progress( $JNPR_ZTP_SCRIPT _ ": serial no = " _ $serial_no );
    expr jcs:syslog( $SYSLOG, $APPNAME _ ": serial no = " _ $serial_no );
    result $serial_no;
}
/* --------------------------------------------------------------------- */
/* Function: set_box_specific_config - CONFIGURES fxp0/em0 and host-name */
/* --------------------------------------------------------------------- */
function ztp_script:set_box_specific_config($platform, $serial_no) {
/* get the apply-macro */
    expr jcs:progress( $JNPR_ZTP_SCRIPT _ ": Setting fxp0 and host-name for lab boxes" );
    expr jcs:syslog( $SYSLOG, $APPNAME _ ": Setting fxp0 and host-name for lab boxes" );
    var $mng_if_name = {
        if (($platform=="acx5096") or ($platform=="acx5048")) {
            expr "em0";
        } else {
             expr "fxp0";
        } /* End If */
    }
    var $re_group_name = {
        if (($platform=="acx5096") or ($platform=="acx5048")) {
            expr "member0";
        } else {
             expr "re0";
        } /* End If */
    }
    
    var $get = <get-configuration> {
         <configuration> {
            <groups> {
               <name> $ZTP_GROUP_BOX;
               <apply-macro> {
                  <name> $serial_no;
                }
            }
        }
    }
    /* getting variables from apply-macro */
    var $got = jcs:execute( $jnx, $get );
    var $fxp_address = $got/groups[name=$ZTP_GROUP_BOX]/apply-macro[name=$serial_no]/data[name='address']/value;
    expr jcs:progress( $JNPR_ZTP_SCRIPT _ ": Current Platform = " _ $platform );
    expr jcs:progress( $JNPR_ZTP_SCRIPT _ ": MNG interface name = " _ $mng_if_name );
    expr jcs:progress( $JNPR_ZTP_SCRIPT _ ": " _ $mng_if_name _ " ip address = " _ $fxp_address );
    if ($got/groups[name=$ZTP_GROUP_BOX]/apply-macro[name=$serial_no]/data[name='host-name']/value) {
        expr jcs:progress( $JNPR_ZTP_SCRIPT _ ": New host-name = " _ $got/groups[name=$ZTP_GROUP_BOX]/apply-macro[name=$serial_no]/data[name='host-name']/value);    
    }
    expr jcs:progress( $JNPR_ZTP_SCRIPT _ ": RE0 Group name = " _ $re_group_name );

    /* Create configuration for group re0 */
    var $options := {
         <commit-options> {
            <log> "setting fxp0 and host-name. For lab only"; 
        }
    }
    var $unit_num = "0";
    var $change = {
        <configuration> {
            <groups> {
                <name> $re_group_name;
                <interfaces> {
                    <interface> {
                        <name> $mng_if_name;
                        <unit> {
                            <name> $unit_num;
                            <family> {
                                <inet> {
                                    <address> $fxp_address;
                                } 
                            }
                        }
                    }
                }
                if ($got/groups[name=$ZTP_GROUP_BOX]/apply-macro[name=$serial_no]/data[name='host-name']/value){
                    <system> {
                         <host-name> $got/groups[name=$ZTP_GROUP_BOX]/apply-macro[name=$serial_no]/data[name='host-name']/value;
                    }
                }
            }
            <apply-groups> $re_group_name;
        }
    }

    /* Load configuration for group re0 */  
    var $results := { call jcs:load-configuration( $action="merge", $commit-options=$options, $configuration=$change, $connection = $jnx ); }
    result $results;
} /* End of the set_box_specific_config function */
/* ------------------------------------------------------------------ */
/* Function rolback_cfg($nRollback) rollback configuration to         */
/* nRollback version                                                  */
/* ------------------------------------------------------------------ */
function ztp_script:rollback_cfg( $cfg_version=0, $rollback_log="Rollback configuration" ) {
    var $rollback_options := {
        <commit-options> {
            <full>;
			<log> $rollback_log;
        }   
    }
    var $rollback_configuration := { call jcs:load-configuration( $commit-options = $rollback_options, $rollback = $cfg_version, $connection = $jnx ); }	
}
/* ------------------------------------------------------------------ */
/* Function: ztp_rollback_phase0 - Rollbacks configuration to basic   */
/* configuration template of Phase 0                                  */
/* ------------------------------------------------------------------ */
function ztp_script:ztp_rollback_phase0() {
	var $lRollback = "ZTD Phase 0";
    mvar $nRollback = 0;
	mvar $bRollback = false();
    var $got_rollback := jcs:execute( $jnx, 'get-commit-information' ); 
	for-each ($got_rollback/commit-history/sequence-number){
	    if (((../client == "autoinstall") || (../log == $lRollback)) && ( not ($bRollback))) {
		   set $bRollback = true();
		   set $nRollback = ../sequence-number;
		   expr jcs:progress( $JNPR_ZTP_SCRIPT _ ": Starting rollback " _ $nRollback _ " to ZTD phase 0" );
		}
	}
    var $rollback = ztp_script:rollback_cfg( $nRollback, $lRollback );
	expr jcs:progress( $JNPR_ZTP_SCRIPT _ ": rollback " _ $nRollback _ " completed successfully" );
    result $nRollback;
}
/* ------------------------------------------------------------------ */
/* Helper routines
/* ------------------------------------------------------------------ */
function ztp_script:file-exists( $filename ) {
    var $ls_file = <file-list> { <path> $filename; }
    var $ls_got = jcs:execute( $jnx, $ls_file );
    var $retval = boolean( $ls_got//file-information );
    result $retval;
}
function ztp_script:file-delete( $filename ) {
    var $do_rm = <file-delete> { <path> $filename; }
    var $did_rm = jcs:execute( $jnx, $do_rm );
    /* @@@ trap error */
    result true();
}
function ztp_script:only_once() {
    if( ztp_script:file-exists( $ZTP_LOCKFILE )) {
        result true();
    } else {
        var $do_lock = <file-put> {
             <filename> $ZTP_LOCKFILE;
             <encoding> 'ascii';
             <file-contents> 'locked';
        }
    var $did_lock = jcs:execute( $jnx, $do_lock );
    result false();
    }
}
function ztp_script:terminate() {
    expr jcs:syslog( $SYSLOG, $APPNAME _ ": SCRIPT-TERMINATE" );
    var $rm_lock = ztp_script:file-delete( $ZTP_LOCKFILE );
    terminate;
}
